dnl                                                      -*-Autoconf-*-
dnl Process this file with autoconf to generate configure.
dnl
dnl The AX_* macros are defined in files in the top-level m4
dnl directory.

#####################################################################
# Set up configuration system
#####################################################################

AC_INIT([gEDA/gaf], [1.10.1], [https://bugs.launchpad.net/geda], [geda-gaf])
AC_PREREQ([2.69])

AC_CONFIG_SRCDIR([libgeda/src/libgeda.c])
AC_CONFIG_AUX_DIR([build-tools])
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_HEADER([config.h])

AM_INIT_AUTOMAKE([parallel-tests color-tests tar-ustar])
AM_SILENT_RULES

AC_GNU_SOURCE # FIXME for some reason this is needed?

#####################################################################
# Do version number magic
#####################################################################

AX_GIT_VERSION([20201216])

# This is used for keeping the ChangeLog files up-to-date
AC_SUBST([CHANGELOG_BASE], [1.0-20070526])

#####################################################################
# Windows/MinGW/Cygwin support
#####################################################################

AX_WINDOWS_FLAGS

#####################################################################
# Check for compiling & linking tools
#####################################################################

AC_PROG_CC
AC_PROG_CPP
m4_ifdef([AM_PROG_AR], [AM_PROG_AR]) dnl Workaround for Automake 1.11

AC_PROG_CC_STDC
if test "X$ac_cv_prog_cc_stdc" = "Xno"; then
  AC_MSG_ERROR([C compiler cannot compile ISO Standard C])
fi

AC_LIBTOOL_WIN32_DLL
AC_DISABLE_STATIC
AC_PROG_LIBTOOL

AM_PROG_CC_C_O

AX_PROG_CC_WARNING([maybe_uninitialized], [maybe-uninitialized])
AX_PROG_CC_WARNING([misleading_indentation], [misleading-indentation])

AX_GCC_FLAGS([-Wall $Werror_maybe_uninitialized_IF_SUPPORTED])

AC_PROG_MKDIR_P
AM_PROG_LEX
AX_PROG_AWK
AX_PROG_GROFF
AC_PATH_PROGS([M4], [gm4 m4], [m4])

#####################################################################
# Internationalisation
#####################################################################

AM_NLS
AM_GNU_GETTEXT([external])
AM_GNU_GETTEXT_VERSION([0.19])
AX_DESKTOP_I18N

#####################################################################
# Check for libraries that use pkg-config
#####################################################################

PKG_PROG_PKG_CONFIG

AX_CHECK_GUILE([2.2.4])

PKG_CHECK_MODULES(GLIB, [glib-2.0 >= 2.58.3], ,
  AC_MSG_ERROR([GLib 2.58.3 or later is required.]))

PKG_CHECK_MODULES(GIO, [gio-2.0 >= 2.58.3], ,
  AC_MSG_ERROR([GIO 2.58.3 or later is required.]))

PKG_CHECK_MODULES(GTK, [gtk+-2.0 >= 2.24.32], ,
  AC_MSG_ERROR([GTK+ 2.24.32 or later is required.]))
GTK_CFLAGS=`echo "$GTK_CFLAGS" | sed "s/-I\\(@<:@^ @:>@*gtk\\)/-isystem\\1/g"`

PKG_CHECK_MODULES(GDK, [gdk-2.0 >= 2.24.32], ,
  AC_MSG_ERROR([GDK 2.24.32 or later is required.]))
GDK_CFLAGS=`echo "$GDK_CFLAGS" | sed "s/-I\\(@<:@^ @:>@*gtk\\)/-isystem\\1/g"`

PKG_CHECK_MODULES(GDK_PIXBUF, [gdk-pixbuf-2.0 >= 2.38.1], ,
  AC_MSG_ERROR([GDK_PIXBUF 2.38.1 or later is required.]))

AX_CHECK_CAIRO

PKG_CHECK_MODULES(PANGOCAIRO, [pangocairo >= 1.42.3], ,
  AC_MSG_ERROR([PANGOCAIRO 1.42.3 or later is required.]))

PKG_CHECK_MODULES(PANGO, [pango >= 1.42.3], ,
  AC_MSG_ERROR([PANGO 1.42.3 or later is required.]))

#####################################################################
# Header files & particular functions
#####################################################################

AC_HEADER_SYS_WAIT

# FIXME these checks are obsolete, because every plausible platform
# provides what they check for.
AC_HEADER_STDC

# FIXME these checks may be pointless for one of three reasons: every
# even vaguely sane libc implementation provides them; we don't use
# any of their functions in favour of GLib alternatives; or we don't
# provide an alternative, so the build will fail anyway.
AC_CHECK_HEADERS([stdlib.h string.h unistd.h])

# FIXME since we don't have a sensible alternative, if these headers
# are missing we should fail. Also need to remove HAVE_ERRNO_H tests
# in the C source code, since if we *don't* have it the build will
# fail anyway.
AC_CHECK_HEADERS([errno.h fcntl.h])

# FIXME On a system without locale.h, the user may have just disabled NLS
# to be able to build.  But are there known systems with working NLS but
# without a locale.h?  We do need to include locale.h on some systems
# to be able to build gschem/src/gschem.c
AC_CHECK_HEADERS([locale.h])

# Check for lrint in math library.
AC_CHECK_LIB([m], [lrint],
             AC_DEFINE([HAVE_LRINT], 1,
                       [If your math library has lrint in it, define this]))

AC_CHECK_LIB([m], [atan2])

AC_CHECK_FUNCS([chown getlogin])

# Check if the getopt header is present
AC_CHECK_HEADERS([getopt.h])
# Check for getopt_long
# On FreeBSD the 'gnugetopt' library is needed.
AC_SEARCH_LIBS([getopt_long], [gnugetopt],
               AC_DEFINE([HAVE_GETOPT_LONG], 1,
                         [Define to 1 if you have the `getopt_long' function.]))

# Check for misc features of awk
AX_AWK_FEATURES

#####################################################################
# Optional things
#####################################################################

# Assertions
AX_OPTION_ASSERT
# Viewing documentation
AX_OPTION_URI_VIEWER
# API documentation
AX_OPTION_DOXYGEN
# XDG desktop files
AX_OPTION_XDG_DATA
# KDE 3 desktop files
AX_OPTION_KDE3_DATA
# XDG database updates
AX_OPTION_XDG_DB
# libfam support
AX_OPTION_FAM
# gattrib
AX_OPTION_GATTRIB

#####################################################################
# Tool-specific setup
#####################################################################

# Where should data files be installed/searched for?
AX_DATA_DIRS
# Where should PCB footprints be searched for?
AX_PCB_DIRS
# Set up libgeda with the correct ld version number
AX_LIBGEDA([47:0:0])
# Set up libgedacairo with the correct ld version number
AX_LIBGEDACAIRO([1:3:0])

#####################################################################
# Recurse into Xorn configuration
#####################################################################

export GEDADATADIR
export GEDARCDIR

AC_CONFIG_SUBDIRS([xorn])

#####################################################################
# Generate output
#####################################################################

AC_CONFIG_FILES([Makefile

                 libgeda/Makefile
                 libgeda/libgeda.pc
                 libgeda/po/domain.mak
                 libgeda/po/Makefile.in
                 libgeda/data/Makefile
                 libgeda/docs/Makefile
                 libgeda/docs/images/Makefile
                 libgeda/include/Makefile
                 libgeda/lib/Makefile
                 libgeda/scheme/Makefile
                 libgeda/src/Makefile

                 libgedacairo/Makefile
                 libgedacairo/libgedacairo.pc

                 gaf/Makefile
                 gaf/po/Makefile.in

                 gschem/Makefile
                 gschem/po/Makefile.in
                 gschem/docs/Makefile
                 gschem/include/Makefile
                 gschem/scripts/Makefile
                 gschem/src/Makefile
                 gschem/examples/Makefile
                 gschem/lib/Makefile
                 gschem/data/Makefile
                 gschem/bitmap/Makefile
                 gschem/tests/Makefile
                 gschem/scheme/Makefile

                 gattrib/Makefile
                 gattrib/po/Makefile.in
                 gattrib/design/Makefile
                 gattrib/docs/Makefile
                 gattrib/include/Makefile
                 gattrib/lib/Makefile
                 gattrib/lib/system-gattribrc
                 gattrib/src/Makefile
                 gattrib/data/Makefile

                 gsymcheck/docs/Makefile
                 gsymcheck/include/Makefile
                 gsymcheck/lib/Makefile
                 gsymcheck/lib/system-gsymcheckrc
                 gsymcheck/po/Makefile.in
                 gsymcheck/src/Makefile
                 gsymcheck/tests/Makefile
                 gsymcheck/Makefile

                 gnetlist-legacy/Makefile
                 gnetlist-legacy/po/Makefile.in
                 gnetlist-legacy/src/Makefile
                 gnetlist-legacy/include/Makefile
                 gnetlist-legacy/scheme/Makefile
                 gnetlist-legacy/examples/Makefile
                 gnetlist-legacy/tests/Makefile
                 gnetlist-legacy/docs/Makefile
                 gnetlist-legacy/docs/vams/Makefile
                 gnetlist-legacy/examples/vams/Makefile
                 gnetlist-legacy/examples/vams/vhdl/Makefile
                 gnetlist-legacy/examples/vams/vhdl/basic-vhdl/Makefile
                 gnetlist-legacy/examples/vams/vhdl/new-vhdl/Makefile
                 gnetlist-legacy/examples/switcap/Makefile
                 gnetlist-legacy/lib/Makefile
                 gnetlist-legacy/utils/Makefile
                 gnetlist-legacy/scripts/Makefile
                 gnetlist-legacy/scripts/annotate.sh
                 gnetlist-legacy/scripts/bom_xref.sh
                 gnetlist-legacy/scripts/bompp.sh
                 gnetlist-legacy/lib/system-gnetlistrc

                 utils/Makefile
                 utils/docs/Makefile
                 utils/examples/Makefile
                 utils/examples/gmk_sym/Makefile
                 utils/examples/gsch2pcb/Makefile
                 utils/examples/tragesym/Makefile
                 utils/gschlas/Makefile
                 utils/gxyrs/Makefile
                 utils/include/Makefile
                 utils/lib/Makefile
                 utils/lib/system-gschlasrc
                 utils/scripts/Makefile
                 utils/src/Makefile
                 utils/tests/gxyrs/Makefile
                 utils/tests/refdes_renum/Makefile

                 symbols/Makefile
                 symbols/documentation/Makefile

                 docs/Makefile
                 docs/scheme-api/Makefile
                 docs/toplevel/Makefile
                 docs/toplevel/gedadocs.html
                 docs/wiki/Makefile

                 examples/Makefile
                 examples/RF_Amp/Makefile
                 examples/RF_Amp/model/Makefile
                 examples/RF_Amp/sym/Makefile
                 examples/TwoStageAmp/Makefile
                 examples/gTAG/Makefile
                 examples/lightning_detector/Makefile
                 examples/SAB/Makefile

                 contrib/Makefile
                 contrib/smash_megafile/Makefile
                 contrib/olib/Makefile
                 contrib/gmk_sym/Makefile
                 contrib/sarlacc_schem/Makefile
                 contrib/scripts/Makefile
                 contrib/scripts/sw2asc
])
AC_OUTPUT
